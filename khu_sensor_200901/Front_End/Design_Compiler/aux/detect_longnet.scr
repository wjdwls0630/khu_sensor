/*
 * This is a dc_shell script to detect long interconnection delay(2ns) in any design.
 * 
 * Date   : 1998. 1. 15    
 * Writer : Hae-jin Song 
 * Version: 1.0
 */

ROOT = get_unix_variable("SEC_SYNOPSYS_AUX")
search_path = ROOT + search_path 

/* setup machine */
include set_machine.scr 

root_name = ""
foreach(top_module, find(design, current_design)){}
current_design = top_module
first = 0 /* delimit top module and submodules: 0-top, 1-sub */

modules = top_module + filter( find(-hierarchy cell,"*"), "@is_hierarchical == true")

echo  -n "" > long_delays /* has long-delay nets */
echo  -n "" > pin2pin_delays /* has pin-to-pin information of the nets */

foreach (submodule, modules) {
	if ( first == 0 ) { /* in case top module */
		current_design submodule
	} else {
		current_instance "/" + top_module + "/" + submodule
		/*  current_design current_reference  */
	}

	set_unix_variable(sub, submodule)
	echo submodule > long_nets

	net_list = {} 
	net_list = find(net, "*")
	report_net net_list -nosplit > all_net.tmp1

	/* get candidate long nets */
	if ( OS == "SunOS" ) {
		sh "nawk -f $SEC_SYNOPSYS_AUX/cut_net.awk all_net.tmp1" > all_net.tmp2
	} else if ( OS == "HP-UX" ) {
		sh "awk -f $SEC_SYNOPSYS_AUX/cut_net.awk all_net.tmp1" > all_net.tmp2
	} else {
		echo "Unsupported Operating System:" OS
	}
	include all_net.tmp2 -quiet

	foreach (subnet, suv_nets ) {
		report_net -connections subnet -verbose -nosplit > net.tmp
		sh "nawk -f $SEC_SYNOPSYS_AUX/long_delay.awk net.tmp"
		sh "cat long_delay.tmp >> long_delays"
		sh "cat pin2pin.tmp >> pin2pin_delays"
	}
	first = 1
}

exist_long_delay = false

if ( OS == "SunOS" ) {
	sh "nawk '{if(NF > 0 ){printf(\"exist_long_delay = true\"); exit}\
     else {printf(\"exist_long_delay = false\"); exit}}' long_delays" > note.tmp
} else if ( OS == "HP-UX" ) {
	sh "awk '{if(NF > 0 ){printf(\"exist_long_delay = true\"); exit}\
     else {printf(\"exist_long_delay = false\"); exit}}' long_delays" > note.tmp
} else {
	echo "Unsupported Operating System:" OS
}

include note.tmp -quiet

/* remove temp files */
sh "\rm long_delay.tmp long_nets net.tmp note.tmp"

if ( exist_long_delay == true ) {
        echo "###########################################################################" > out_mesg
	echo  "There are nets having large delay( >=2ns) in"top_module "." >> out_mesg
   echo  "If you want to modify the value, edit \"long_delays\" file"  >> out_mesg
   echo  "and run it using following command." >> out_mesg 
	echo  "\"include new_delay.scr -quiet\""  >> out_mesg
        echo "###########################################################################" >> out_mesg

} else {
        echo "###########################################################################" > out_mesg
	echo  " There is no net having large delay( >=2 ns) in "  top_module  "." >> out_mesg
        echo "###########################################################################" >> out_mesg
}

sh "cat out_mesg"
sh "\rm out_mesg"

current_design = top_module
current_instance
