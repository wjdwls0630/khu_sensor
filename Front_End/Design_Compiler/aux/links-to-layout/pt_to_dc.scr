#!/bin/csh -f
# Author: Hae-jin Song
# Date: 2000. 4. 24
# Version: v1.2

# Background: 
#   Some timing arcs must be disabled in logic gates through gated clock and multiple clock on Design Compiler.
#
# Prerequisite:
#   To do this script you have to run check_timing command after applying all
#   constraints on DC, and to run report_disable_timing command after applying
#   all constraints on PT.
#
# This is a script to generate constraints for Design Compiler with
#   check_timing result of DC and set_disable_timing result of PT.
#

if ( $#argv == 3 ) then
   if ( $3 != "-hp")  goto usage
else if ( $#argv != 2 ) then
   goto usage
endif

if ( $#argv == 3 ) then
   setenv AWK awk
else
   setenv AWK nawk
endif


#setenv DC_check_timing_rt check_timing.bist.rt
setenv DC_check_timing_rt $1
#setenv PT_report_disable_timing_rt report_disable_timing.rt
setenv PT_disable_timing $2

cat <<! >! rm_end_pin.awk
BEGIN {
	cnt = 0
}
{
	if ( \$1 == "Information:" ) {
		cnt = 0
	} else if (\$7 == "Gating" && \$8 == "Input" ) {
		cnt ++
	} else if ( cnt >0 && match(\$1,"---")){
		cnt ++
	} else if ( cnt >1 && NF > 0 ) {
		nl = split(\$1,a,"/")
		for(i=1; i< nl; i++) {
			if ( i != nl-1 ) printf("%s/",a[i])
			else printf("%s\n",a[i])
		}	
	}
}
!

$AWK -f rm_end_pin.awk $DC_check_timing_rt | sort -u > ! check_timing.tmp

if ( -z check_timing.tmp ) then
	echo "*** Error: illegal file format."
	echo "           $1 is not a file generated by Design Compiler."
   goto usage
endif

echo "1. Check_timing result was sorted." 

$AWK '{ if($5 == "c") print $0}' $PT_disable_timing | sort -u \
	>! disable_timing.tmp

if ( -z disable_timing.tmp ) then
	echo "*** Error: illegal file format."
	echo "           $2 is not a file generated by PrimeTime."
   goto usage
endif

echo "2. Report_disable_timing result was sorted."

join -j1 1 -j2 1 -o 1.1 1.2 1.3 disable_timing.tmp check_timing.tmp > ! joined.tmp
echo "3. Joined the sorted two files."

$AWK '{ print "set_disable_timing",$1,"-from",$2,"-to",$3}' joined.tmp \
 >! disable_timing_for_DC.dcsh
echo ""
echo "*** Information: Completed successfully\!"
echo "*** Generate dc_shell script file: disable_timing_for_DC.dcsh"
echo "*** Include disable_timing_for_DC.dcsh on Design Compiler."
echo "*** (example)  dc_shell> include disable_timing_for_DC.dcsh "

\rm check_timing.tmp joined.tmp disable_timing.tmp rm_end_pin.awk

exit

usage:
	echo "Usage: pt_to_dc.scr [check_timing_on_DC report_disable_timing_on_PT] [-hp]"
   echo ""      
   echo "   Options:"
   echo "           check_timing_on_DC: generated by check_timing command on DC"
   echo "           report_disable_timing_on_PT: generated by report_disable_timing"
   echo "                               command on PT"
   echo ""
   echo "           -hp : for HP machine"
   echo ""
   echo " *** Check if you have check_timing result of DC and report_disable_timing"
   echo "     result of PT."
	echo ""
